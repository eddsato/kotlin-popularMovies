apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.eddsato.popularmovies"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildTypes.each {
        Properties properties = new Properties()
        properties.load(project.rootProject.file("local.properties").newDataInputStream())
        def tmdbApiKey = properties.getProperty("tmdb_api_key", "")

        it.buildConfigField 'String', "TMDB_API_KEY", tmdbApiKey

        it.resValue 'string', "api_key", tmdbApiKey
    }

    androidExtensions {
        experimental = true
    }
}

dependencies {
    def retrofitVersion = "2.5.0"
    def picassoVersion = "2.5.2"

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    //Support Libraries
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    //REST Interactions
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"

    //JSON Parsing
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    //Logging
    implementation 'com.jakewharton.timber:timber:4.7.1'

    //Events
    implementation 'de.greenrobot:eventbus:2.4.0'

    //Image Loading
    implementation("com.squareup.picasso:picasso:$picassoVersion") {
        exclude group: "com.android.support"
    }

    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'

    //Testing
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0'
}
